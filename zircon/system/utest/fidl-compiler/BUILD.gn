# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# The generation depends only on the source, so do it only once.
if (current_toolchain == default_toolchain) {
  action("gen-examples") {
    visibility = [ ":examples" ]
    outputs = [ "$target_gen_dir/examples.cc" ]
    sources = [
      # find zircon/tools/fidl/examples -type f | sed -e 's+^zircon\(.*\)$+"$zx\1",+' | sort
      "$zx/tools/fidl/examples/alignment.test.fidl",
      "$zx/tools/fidl/examples/bits.test.fidl",
      "$zx/tools/fidl/examples/empty.test.fidl",
      "$zx/tools/fidl/examples/empty_struct.test.fidl",
      "$zx/tools/fidl/examples/enums.test.fidl",
      "$zx/tools/fidl/examples/errors.test.fidl",
      "$zx/tools/fidl/examples/events.test.fidl",
      "$zx/tools/fidl/examples/example-0.test.fidl",
      "$zx/tools/fidl/examples/example-1.test.fidl",
      "$zx/tools/fidl/examples/example-10.test.fidl",
      "$zx/tools/fidl/examples/example-2.test.fidl",
      "$zx/tools/fidl/examples/example-3.test.fidl",
      "$zx/tools/fidl/examples/example-4.test.fidl",
      "$zx/tools/fidl/examples/example-5.test.fidl",
      "$zx/tools/fidl/examples/example-6.test.fidl",
      "$zx/tools/fidl/examples/example-7.test.fidl",
      "$zx/tools/fidl/examples/example-8.test.fidl",
      "$zx/tools/fidl/examples/example-9.test.fidl",
      "$zx/tools/fidl/examples/library-a/point.test.fidl",
      "$zx/tools/fidl/examples/library-b/view.test.fidl",
      "$zx/tools/fidl/examples/protocol-ordinals.test.fidl",
      "$zx/tools/fidl/examples/simple.test.fidl",
      "$zx/tools/fidl/examples/tables.test.fidl",
      "$zx/tools/fidl/examples/test.test.fidl",
      "$zx/tools/fidl/examples/types.test.fidl",
      "$zx/tools/fidl/examples/xunion.test.fidl",

      # manual
      "testdata/badformat.fidl.noformat",
      "testdata/doc_comments.test.fidl",
      "testdata/goodformat.test.fidl",
    ]
    script = "gen-examples.sh"
    args = rebase_path(outputs, root_build_dir) +
           rebase_path(sources, root_build_dir)
  }

  action("gen-goldens") {
    visibility = [ ":goldens" ]
    outputs = [ "$target_gen_dir/goldens.cc" ]
    sources = [
      # find zircon/tools/fidl/{testdata,goldens} -type f | sed -e 's+^zircon\(.*\)$+"$zx\1",+' | grep -v README | grep -v regen | sort
      "$zx/tools/fidl/goldens/bits.test.json.golden",
      "$zx/tools/fidl/goldens/bits.test.tables.c.golden",
      "$zx/tools/fidl/goldens/byte_and_bytes.test.json.golden",
      "$zx/tools/fidl/goldens/byte_and_bytes.test.tables.c.golden",
      "$zx/tools/fidl/goldens/constants.test.json.golden",
      "$zx/tools/fidl/goldens/constants.test.tables.c.golden",
      "$zx/tools/fidl/goldens/consts.test.json.golden",
      "$zx/tools/fidl/goldens/consts.test.tables.c.golden",
      "$zx/tools/fidl/goldens/doc_comments.test.json.golden",
      "$zx/tools/fidl/goldens/doc_comments.test.tables.c.golden",
      "$zx/tools/fidl/goldens/empty_struct.test.json.golden",
      "$zx/tools/fidl/goldens/empty_struct.test.tables.c.golden",
      "$zx/tools/fidl/goldens/enum.test.json.golden",
      "$zx/tools/fidl/goldens/enum.test.tables.c.golden",
      "$zx/tools/fidl/goldens/error.test.json.golden",
      "$zx/tools/fidl/goldens/error.test.tables.c.golden",
      "$zx/tools/fidl/goldens/escaping.test.json.golden",
      "$zx/tools/fidl/goldens/escaping.test.tables.c.golden",
      "$zx/tools/fidl/goldens/foreign_type_in_response_used_through_compose.test.json.golden",
      "$zx/tools/fidl/goldens/handles.test.json.golden",
      "$zx/tools/fidl/goldens/handles.test.tables.c.golden",
      "$zx/tools/fidl/goldens/handles_in_types.test.json.golden",
      "$zx/tools/fidl/goldens/handles_in_types.test.tables.c.golden",
      "$zx/tools/fidl/goldens/inheritance.test.json.golden",
      "$zx/tools/fidl/goldens/inheritance.test.tables.c.golden",
      "$zx/tools/fidl/goldens/inheritance_with_recursive_decl.test.json.golden",
      "$zx/tools/fidl/goldens/inheritance_with_recursive_decl.test.tables.c.golden",
      "$zx/tools/fidl/goldens/placement_of_attributes.test.json.golden",
      "$zx/tools/fidl/goldens/protocol_request.test.json.golden",
      "$zx/tools/fidl/goldens/protocol_request.test.tables.c.golden",
      "$zx/tools/fidl/goldens/protocols.test.json.golden",
      "$zx/tools/fidl/goldens/protocols.test.tables.c.golden",
      "$zx/tools/fidl/goldens/request_flexible_envelope.test.json.golden",
      "$zx/tools/fidl/goldens/request_flexible_envelope.test.tables.c.golden",
      "$zx/tools/fidl/goldens/service.test.json.golden",
      "$zx/tools/fidl/goldens/service.test.tables.c.golden",
      "$zx/tools/fidl/goldens/struct.test.json.golden",
      "$zx/tools/fidl/goldens/struct.test.tables.c.golden",
      "$zx/tools/fidl/goldens/struct_default_value_enum_library_reference.test.json.golden",
      "$zx/tools/fidl/goldens/table.test.json.golden",
      "$zx/tools/fidl/goldens/table.test.tables.c.golden",
      "$zx/tools/fidl/goldens/transitive_dependencies.test.json.golden",
      "$zx/tools/fidl/goldens/transitive_dependencies_compose.test.json.golden",
      "$zx/tools/fidl/goldens/type_aliases.test.json.golden",
      "$zx/tools/fidl/goldens/union.test.json.golden",
      "$zx/tools/fidl/goldens/union.test.tables.c.golden",
      "$zx/tools/fidl/goldens/union_sandwich.test.json.golden",
      "$zx/tools/fidl/goldens/union_sandwich.test.tables.c.golden",
      "$zx/tools/fidl/goldens/xunion.test.json.golden",
      "$zx/tools/fidl/goldens/xunion.test.tables.c.golden",
      "$zx/tools/fidl/testdata/bits.test.fidl",
      "$zx/tools/fidl/testdata/byte_and_bytes.test.fidl",
      "$zx/tools/fidl/testdata/constants.test.fidl",
      "$zx/tools/fidl/testdata/consts.test.fidl",
      "$zx/tools/fidl/testdata/doc_comments.test.fidl",
      "$zx/tools/fidl/testdata/empty_struct.test.fidl",
      "$zx/tools/fidl/testdata/enum.test.fidl",
      "$zx/tools/fidl/testdata/error.test.fidl",
      "$zx/tools/fidl/testdata/escaping.test.fidl",
      "$zx/tools/fidl/testdata/foreign_type_in_response_used_through_compose/bottom.test.fidl",
      "$zx/tools/fidl/testdata/foreign_type_in_response_used_through_compose/middle.test.fidl",
      "$zx/tools/fidl/testdata/foreign_type_in_response_used_through_compose/order.txt",
      "$zx/tools/fidl/testdata/foreign_type_in_response_used_through_compose/top.test.fidl",
      "$zx/tools/fidl/testdata/handles.test.fidl",
      "$zx/tools/fidl/testdata/handles_in_types.test.fidl",
      "$zx/tools/fidl/testdata/inheritance.test.fidl",
      "$zx/tools/fidl/testdata/inheritance_with_recursive_decl.test.fidl",
      "$zx/tools/fidl/testdata/placement_of_attributes/example.test.fidl",
      "$zx/tools/fidl/testdata/placement_of_attributes/exampleusing.test.fidl",
      "$zx/tools/fidl/testdata/placement_of_attributes/order.txt",
      "$zx/tools/fidl/testdata/protocol_request.test.fidl",
      "$zx/tools/fidl/testdata/protocols.test.fidl",
      "$zx/tools/fidl/testdata/request_flexible_envelope.test.fidl",
      "$zx/tools/fidl/testdata/service.test.fidl",
      "$zx/tools/fidl/testdata/struct.test.fidl",
      "$zx/tools/fidl/testdata/struct_default_value_enum_library_reference/dependent.test.fidl",
      "$zx/tools/fidl/testdata/struct_default_value_enum_library_reference/example.test.fidl",
      "$zx/tools/fidl/testdata/struct_default_value_enum_library_reference/order.txt",
      "$zx/tools/fidl/testdata/table.test.fidl",
      "$zx/tools/fidl/testdata/transitive_dependencies/bottom.test.fidl",
      "$zx/tools/fidl/testdata/transitive_dependencies/middle.test.fidl",
      "$zx/tools/fidl/testdata/transitive_dependencies/order.txt",
      "$zx/tools/fidl/testdata/transitive_dependencies/top.test.fidl",
      "$zx/tools/fidl/testdata/transitive_dependencies_compose/bottom.test.fidl",
      "$zx/tools/fidl/testdata/transitive_dependencies_compose/middle.test.fidl",
      "$zx/tools/fidl/testdata/transitive_dependencies_compose/order.txt",
      "$zx/tools/fidl/testdata/transitive_dependencies_compose/top.test.fidl",
      "$zx/tools/fidl/testdata/type_aliases/example.test.fidl",
      "$zx/tools/fidl/testdata/type_aliases/order.txt",
      "$zx/tools/fidl/testdata/type_aliases/someotherlibrary.test.fidl",
      "$zx/tools/fidl/testdata/union.test.fidl",
      "$zx/tools/fidl/testdata/union_sandwich.test.fidl",
      "$zx/tools/fidl/testdata/xunion.test.fidl",
    ]
    script = "gen-goldens.py"
    args = rebase_path(outputs, root_build_dir) +
           rebase_path(sources, root_build_dir)
  }
} else {
  zx_test("fidl-compiler") {
    sources = [
      "array_tests.cc",
      "attributes_tests.cc",
      "bits_tests.cc",
      "coded_types_generator_tests.cc",
      "consts_tests.cc",
      "declaration_order_tests.cc",
      "enums_tests.cc",
      "errors_tests.cc",
      "flat_ast_tests.cc",
      "formatter_tests.cc",
      "handle_tests.cc",
      "json_findings_tests.cc",
      "json_generator_tests.cc",
      "lint_findings_tests.cc",
      "lint_tests.cc",
      "main.cc",
      "ordinals_tests.cc",
      "parsing_tests.cc",
      "protocol_tests.cc",
      "recursion_detector_tests.cc",
      "service_tests.cc",
      "span_tests.cc",
      "strictness_tests.cc",
      "structs_tests.cc",
      "table_tests.cc",
      "type_alias_tests.cc",
      "types_tests.cc",
      "typeshape_tests.cc",
      "union_tests.cc",
      "using_tests.cc",
      "utils_tests.cc",
      "virtual_source_tests.cc",
      "visitor_unittests.cc",
      "xunion_tests.cc",
    ]
    deps = [
      ":examples",
      ":goldens",
      "$zx/system/ulib/unittest",
      "$zx/system/ulib/zxtest",
      "$zx/third_party/ulib/uboringssl",
      "$zx/tools/fidl",
    ]
  }

  source_set("examples") {
    gen_label = ":gen-examples($default_toolchain)"
    gen_dir = get_label_info(gen_label, "target_gen_dir")
    include_dirs = [ "." ]
    sources = [ "$gen_dir/examples.cc" ]
    deps = [ gen_label ]
  }

  source_set("goldens") {
    gen_label = ":gen-goldens($default_toolchain)"
    gen_dir = get_label_info(gen_label, "target_gen_dir")
    include_dirs = [ "." ]
    sources = [ "$gen_dir/goldens.cc" ]
    deps = [ gen_label ]
  }
}
